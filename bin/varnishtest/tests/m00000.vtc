varnishtest "Test std, vtc & debug vmod"

server s1 {
	rxreq
	expect req.http.encrypted == "ROT52"
	txresp -hdr "foo: bAr" -hdr "bar: fOo" -bodylen 4
} -start

varnish v1 -vcl+backend {
	import std;
	import debug;
	import vtc;

	sub vcl_init {
		new obj = debug.obj();
	}

	sub vcl_recv {
		debug.rot52(req);
	}

	sub vcl_deliver {
		set resp.http.foo = std.toupper(resp.http.foo);
		set resp.http.bar = std.tolower(resp.http.bar);
		set resp.http.who = debug.author(mithrandir);
		set resp.http.really = debug.author();
		set resp.http.what = vtc.typesize("dfijlopsz");
		set resp.http.not = vtc.typesize("*");
		debug.test_priv_call();
		debug.test_priv_vcl();
		obj.test_priv_call();
		obj.test_priv_vcl();
		std.log("VCL" + " initiated " + "log");
		std.syslog(8 + 7, "Somebody runs varnishtest");
		debug.rot52(resp);
		set resp.http.f_poly_int = debug.f_poly(42);
		set resp.http.f_poly_real = debug.f_poly(30.12);
		set resp.http.f_poly_string = debug.f_poly(33s + 3m);
		set resp.http.m_poly_int = obj.m_poly(42);
		set resp.http.m_poly_real = obj.m_poly(30.12);
		set resp.http.m_poly_string = obj.m_poly(33s + 3m);
	}
} -start

client c1 {
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.bodylen == "4"
	expect resp.http.foo == "BAR"
	expect resp.http.bar == "foo"
	expect resp.http.who == "Tollef"
	expect resp.http.really == "Poul-Henning"
	expect resp.http.encrypted == "ROT52"
	expect resp.http.what >= 16
	expect resp.http.not == -1
} -run

logexpect l1 -v v1 -g raw -d 1 {
	expect * 1001 VCL_call		{^DELIVER}
	expect 0 =    RespUnset	{^foo: bAr}
	expect 0 =    RespHeader	{^foo: BAR}
	expect 0 =    RespUnset	{^bar: fOo}
	expect 0 =    RespHeader	{^bar: foo}
	expect 0 =    RespHeader	{^who: Tollef}
	expect 0 =    RespHeader	{^really: Poul-Henning}
	expect 0 =    RespHeader	{^what: [1-9][0-9]}
	expect 0 =    RespHeader	{^not: -1}
	expect 0 =    VCL_Log		{^VCL initiated log}
	expect 0 =    RespHeader	{^Encrypted: ROT52}
	expect 0 =    Debug		{^f_poly_int\(42\)}
	expect 0 =    RespHeader	{^f_poly_int: 42$}
	expect 0 =    Debug		{^f_poly_real\(30.120000\)}
	expect 0 =    RespHeader	{^f_poly_real: 30$}
	expect 0 =    Debug		{^f_poly_string\(213.000\)}
	expect 0 =    RespHeader	{^f_poly_string: 213$}
	expect 0 =    Debug		{^m_poly_int\(42\)}
	expect 0 =    RespHeader	{^m_poly_int: 42$}
	expect 0 =    Debug		{^m_poly_real\(30.120000\)}
	expect 0 =    RespHeader	{^m_poly_real: 30$}
	expect 0 =    Debug		{^m_poly_string\(213.000\)}
	expect 0 =    RespHeader	{^m_poly_string: 213$}
	expect 0 =    VCL_return	{^deliver}
} -start

logexpect l1 -wait

varnish v1 -errvcl {Wrong enum value.  Expected one of:} {
	import debug;
	sub vcl_deliver {
		set resp.http.who = debug.author(ENUM);
	}
}

varnish v1 -errvcl {Wrong enum value.  Expected one of:} {
	import debug;
	sub vcl_deliver {
		set resp.http.who = debug.author(slink);
	}
}

varnish v1 -errvcl {Expression has type STRING, expected REAL} {
	import std;
	sub vcl_deliver {
		set resp.http.who = std.random("foo", "bar");
	}
}
