varnishtest "Stevedore variables and BYTES type test"


server s1 {
	rxreq
	txresp -bodylen 4
	rxreq
	txresp -bodylen 5
} -start

varnish v1 -syntax 4.0 -vcl+backend {

	sub vcl_backend_response {
		set beresp.http.foo =
		    storage.Transient.used_space +
		    1 B + 1 KiB + 1 MiB + 1GiB + 1TiB;
		if (bereq.url == "/foo") {
			set beresp.storage = storage.Transient;
		}
	}
	sub vcl_deliver {
		set resp.http.bar = storage.Transient.used_space > 0B;
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 1100586419201
	expect resp.http.bar == false

	txreq -url /foo
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 1100586419201
	expect resp.http.bar == true
} -run

varnish v1 -syntax 4.0 -errvcl {Expected bytes unit got '"X"'} {
	sub vcl_recv {
		if (storage.Transient.free_space > 4 "X") {
		}
	}
}
varnish v1 -syntax 4.0 -errvcl {Unknown bytes unit} {
	sub vcl_recv {
		if (storage.Transient.free_space > 4 X) {
		}
	}
}
