varnishtest "Chunked with no space for iov's on workspace"

server s1 -repeat 2 {
	rxreq
	txresp -nolen -hdr "Transfer-encoding: chunked"
	delay 1
	chunkedlen 10
	delay .1
	chunkedlen 10
	delay .1
	chunkedlen 0
} -start

varnish v1 -vcl+backend {
	import vtc;

	sub vcl_deliver {
		# struct v1l is 13 - 15 pointer-sizes,
		# an iovec should be two pointer-sizes
		#
		# we want to hit the case where we got just not
		# enough space for three iovecs
		if (req.url == "/1") {
			vtc.workspace_alloc(session,
			    -1 * vtc.typesize("p") * (13 + 4));
		} else {
			vtc.workspace_alloc(session,
			    -1 * vtc.typesize("p") * (15 + 6));
		}
		set resp.http.foo = vtc.workspace_free(session);
	}
} -start

client c1 {
	txreq -url /1
	rxresp
	expect resp.status == 500
} -run
client c1 {
	txreq -url /2
	rxresp
	expect resp.status == 200
} -run

